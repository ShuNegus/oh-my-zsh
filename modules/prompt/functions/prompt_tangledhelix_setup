# A theme based on steeef
# 'tangledhelix' variant by Dan Lowe <dan@tangledhelix.com>

function prompt_tangledhelix_promptchar {
  git branch >/dev/null 2>/dev/null && echo '±' && return
  hg root >/dev/null 2>/dev/null && echo '☿' && return
  echo '○'
}

function virtualenv_info {
  if [[ -n "$VIRTUAL_ENV" ]]; then
    print "(${VIRTUAL_ENV:t}) "
  fi
}

function prompt_tangledhelix_precmd {
  # Check for untracked files or updated submodules since vcs_info doesn't.
  if [[ ! -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    fmt_branch="on ${__PROMPT_TANGLEDHELIX_COLORS[1]}%b %f%u%c${__PROMPT_TANGLEDHELIX_COLORS[4]}●%f"
  else
    fmt_branch="on ${__PROMPT_TANGLEDHELIX_COLORS[1]}%b %f%u%c"
  fi
  zstyle ':vcs_info:*:prompt:*' formats "${fmt_branch}"

  if [[ "$(git status 2>/dev/null)" =~ ' ahead of ' ]]; then
    fmt_git_ahead=" ⬆"
  else
    fmt_git_ahead=""
  fi

  vcs_info 'prompt'
}

function prompt_tangledhelix_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_tangledhelix_precmd

  __PROMPT_TANGLEDHELIX_COLORS=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
  )

  # Enable VCS systems you use.
  zstyle ':vcs_info:*' enable bzr git hg svn

  # check-for-changes can be really slow.
  # You should disable it if you work with large repositories.
  zstyle ':vcs_info:*:prompt:*' check-for-changes true

  # Formats:
  # %b - branchname
  # %u - unstagedstr (see below)
  # %c - stagedstr (see below)
  # %a - action (e.g. rebase-i)
  # %R - repository path
  # %S - path in the repository
  local fmt_branch="on ${__PROMPT_TANGLEDHELIX_COLORS[1]}%b %f%u%c"
  local fmt_action="(${__PROMPT_TANGLEDHELIX_COLORS[5]}%a%f)"
  local fmt_unstaged="${__PROMPT_TANGLEDHELIX_COLORS[2]}●%f"
  local fmt_staged="${__PROMPT_TANGLEDHELIX_COLORS[5]}●%f"

  local _prompt_m
  if [[ -n "$(command -v ec2-metadata)" ]]; then
    _prompt_m=${${=$(ec2-metadata -i)}[2]}
  elif [[ $(uname -n) =~ '^[^.]+\.[^.]+\.[^.]+\.[^.]' ]]; then
    _prompt_m="%2m"
  else
    _prompt_m="%m"
  fi

  zstyle ':vcs_info:*:prompt:*' unstagedstr   "${fmt_unstaged}"
  zstyle ':vcs_info:*:prompt:*' stagedstr     "${fmt_staged}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch}${fmt_action}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  PROMPT="
${__PROMPT_TANGLEDHELIX_COLORS[3]}%n%f at ${__PROMPT_TANGLEDHELIX_COLORS[2]}${_prompt_m}%f in ${__PROMPT_TANGLEDHELIX_COLORS[5]}%~%f "'${vcs_info_msg_0_}${fmt_git_ahead}'"
"'$(virtualenv_info)'"\$(prompt_tangledhelix_promptchar) "
}

prompt_tangledhelix_setup "$@"

