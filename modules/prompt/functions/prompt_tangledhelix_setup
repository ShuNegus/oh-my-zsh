
# The guts of sorin married to the look of steeef, plus tweaks.
# Designed for Solarized Dark.

if [[ $TERM = *256color* ]]; then
  __PROMPT_TANGLEDHELIX_COLORS=(
    "%F{33}"    # turquoise
    "%F{125}"   # purple
    "%F{166}"   # orange
    "%F{64}"    # green
  )
else
  __PROMPT_TANGLEDHELIX_COLORS=(
    "%F{cyan}"
    "%F{magenta}"
    "%F{yellow}"
    "%F{green}"
  )
fi

function prompt_tangledhelix_promptchar {
  [[ $UID -eq 0 || $EUID -eq 0 ]] && echo '#' || echo '%%'
}

function prompt_tangledhelix_vcsinfo {

  local __svn_info="$(svn info 2>/dev/null)"

  if [[ -n "$__svn_info" ]]; then

    local __svn_rev="r$(svn info | awk '/^Revision:/ {print $2}')"
    local __svn_url="$(svn info | awk '/^URL:/ {print $2}')"
    local __svn_loc

    if [[ $__svn_url =~ '/trunk/' ]]; then
      __svn_loc=' on %F{blue}trunk%f'
    elif [[ $__svn_url =~ '/branches/([^/]*)' ]]; then
      __svn_loc=" on ${__PROMPT_TANGLEDHELIX_COLORS[3]}${match[1]}%f"
    fi

    __PROMPT_TANGLEDHELIX_VCSINFO=" at %F{blue}svn%f:${__PROMPT_TANGLEDHELIX_COLORS[1]}$__svn_rev%f$__svn_loc"

    return
  fi

  test -d CVS && __PROMPT_TANGLEDHELIX_VCSINFO=" in %F{blue}CVS%f" && return

  unset __PROMPT_TANGLEDHELIX_VCSINFO
}

function prompt_tangledhelix_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
  prompt_tangledhelix_vcsinfo
}

function prompt_tangledhelix_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_tangledhelix_precmd

  zstyle ':omz:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':omz:module:editor:keymap:primary' overwrite '%F{red}♺%f '
  zstyle ':omz:module:editor:keymap' alternate '%F{yellow}❯%f%B%F{red}❯%f%b%F{red}❯%f '
  zstyle ':omz:module:git' action ':%%B%F{yellow}%s%f%%b'
  zstyle ':omz:module:git' added '%%B%F{green}✚%f%%b '
  zstyle ':omz:module:git' ahead '%%B%F{yellow}⬆%f%%b '
  zstyle ':omz:module:git' behind '%%B%F{yellow}⬇%f%%b '
  zstyle ':omz:module:git' branch ':${__PROMPT_TANGLEDHELIX_COLORS[1]}%b%f'
  zstyle ':omz:module:git' commit ':%F{green}%.7c%f'
  zstyle ':omz:module:git' deleted '%%B%F{red}✖%f%%b '
  zstyle ':omz:module:git' modified '%%B%F{blue}✱%f%%b '
  zstyle ':omz:module:git' position ':%F{green}%p%f'
  zstyle ':omz:module:git' renamed '%%B%F{magenta}➜%f%%b '
  zstyle ':omz:module:git' stashed '%%B%F{cyan}✭%f%%b '
  zstyle ':omz:module:git' unmerged '%%B%F{yellow}═%f%%b '
  zstyle ':omz:module:git' untracked '%%B%F{white}◼%f%%b '
  zstyle ':omz:module:git' info \
    'prompt'  ' on %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  if [[ -n "$(command -v ec2-metadata)" ]]; then
    __PROMPT_TANGLEDHELIX_HOST=${${=$(ec2-metadata -i)}[2]}
  elif [[ $(uname -n) =~ '^[^.]+\.[^.]+\.[^.]+\.[^.]' ]]; then
    __PROMPT_TANGLEDHELIX_HOST='%2m'
  else
    __PROMPT_TANGLEDHELIX_HOST='%m'
  fi

  PROMPT='
${__PROMPT_TANGLEDHELIX_COLORS[2]}%n%f at ${__PROMPT_TANGLEDHELIX_COLORS[3]}${__PROMPT_TANGLEDHELIX_HOST}%f in ${__PROMPT_TANGLEDHELIX_COLORS[4]}%~%f${git_info:+${(e)git_info[prompt]}}${__PROMPT_TANGLEDHELIX_VCSINFO}
${git_info[rprompt]}%(?::%F{red}!%f )${editor_info[keymap]}${editor_info[overwrite]}%B%(!.%F{red}.%F{green})$(prompt_tangledhelix_promptchar)%f%b '
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_tangledhelix_setup "$@"

